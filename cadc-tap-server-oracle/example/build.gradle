plugins {
    id 'war'
}

repositories {
    mavenLocal()
    jcenter()

    maven {
        name 'maven.oracle.com'
        url 'https://maven.oracle.com'
        credentials {
            username = System.getenv("MAVEN_ORACLE_USERNAME") || mavenOracleUsername
            password = System.getenv("MAVEN_ORACLE_PASSWORD") || mavenOraclePassword
        }
    }
}

group = 'org.opencadc'
version = '1000'

war {
    archiveName = 'tap-example##' + project.version + '.war'
}

configurations {
    intTestCompile.extendsFrom testCompile
    intTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile 'com.oracle:ojdbc6:11.2.0.3'
    compile 'org.opencadc:cadc-tap-server-oracle:[1.0,)'
    compile 'org.opencadc:cadc-tap-server:[1.1.3,)'
    compile 'org.opencadc:cadc-adql:[1.1,)'

    testCompile 'junit:junit:4.+'
}

sourceSets {
    intTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/intTest/java')
        }

        // set the intTest resource directory
        resources.srcDir file('src/intTest/resources')
    }
}

tasks.withType(Test) {
    // reset the report destinations so that intTests go to their
    // own page
    //reports.html.destination = file("${reporting.baseDir}/${name}")
    reports.html.destination = file(reporting.baseDir.getAbsolutePath() + '/' + name)

    // Assign all Java system properties from
    // the command line to the tests
    systemProperties System.properties as Map<String, ?>
}

task intTest(type: Test) {
    // set the configuration context
    testClassesDir = sourceSets.intTest.output.classesDir
    classpath = sourceSets.intTest.runtimeClasspath

    // run the tests always
    outputs.upToDateWhen { false }
}
